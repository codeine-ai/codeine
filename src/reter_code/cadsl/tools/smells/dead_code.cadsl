# dead_code - Detect unused classes, methods, and functions
#
# Dead code adds maintenance burden and can confuse developers.
# It should be removed to keep the codebase clean.

detector dead_code(category="maintenance", severity="low") {
    """Detect unused classes, methods, and functions."""

    param include_private: bool = false;
    param limit: int = 100;

    # Only use method to avoid double-counting (entities have both method and method)
    # Check both 'calls' (static resolution) and 'maybe-calls' (probabilistic resolution)
    reql {
        SELECT ?e ?name ?file ?line
        WHERE {
            ?e type method .
            ?e has-name ?name .
            ?e is-in-file ?file .
            ?e is-at-line ?line .
            FILTER NOT EXISTS {
                { ?caller calls ?e } UNION { ?caller maybe-calls ?e }
            }
            FILTER ( !REGEX(?name, "^__") )
            FILTER ( {include_private} || !REGEX(?name, "^_") )
            FILTER ( !REGEX(?file, "test") )
        }
        ORDER BY ?file ?line
        LIMIT {limit}
    }
    | select { name, file, line, qualified_name: e }
    | map {
        ...row,
        issue: "dead_code",
        message: "Unused method '{name}' - no callers found",
        suggestion: "Consider removing if truly unused"
    }
    | emit { findings }
}
